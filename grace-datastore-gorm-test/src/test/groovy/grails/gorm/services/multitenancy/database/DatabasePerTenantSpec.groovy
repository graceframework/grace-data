/*
 * Copyright 2010-2025 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package grails.gorm.services.multitenancy.database

import spock.lang.AutoCleanup
import spock.lang.IgnoreIf
import spock.lang.Shared
import spock.lang.Specification

import org.grails.datastore.mapping.config.Settings
import org.grails.datastore.mapping.core.DatastoreUtils
import org.grails.datastore.mapping.core.connections.ConnectionSource
import org.grails.datastore.mapping.multitenancy.MultiTenancySettings
import org.grails.datastore.mapping.multitenancy.exceptions.TenantNotFoundException
import org.grails.datastore.mapping.multitenancy.resolvers.SystemPropertyTenantResolver
import org.grails.datastore.mapping.simple.SimpleMapDatastore

/**
 * Created by graemerocher on 05/04/2017.
 */
class DatabasePerTenantSpec extends Specification {

    @Shared
    @AutoCleanup
    SimpleMapDatastore datastore = new SimpleMapDatastore(
            DatastoreUtils.createPropertyResolver([(Settings.SETTING_MULTI_TENANCY_MODE)   : MultiTenancySettings.MultiTenancyMode.DATABASE,
                                                   (Settings.SETTING_MULTI_TENANT_RESOLVER): new SystemPropertyTenantResolver(),
                                                   (Settings.SETTING_DB_CREATE)            : 'create-drop']),
            [ConnectionSource.DEFAULT, 'foo', 'bar'],
            Book
    )

    @Shared
    IBookService bookDataService = datastore.getService(IBookService)

    //Ignore test on Github Workflow because of inconsistent behavior
    @IgnoreIf({ System.getenv('GITHUB_WORKFLOW') })
    void 'Test database per tenant'() {
        when: 'When there is no tenant'
        Book.count()

        then: 'You still get an exception'
        thrown(TenantNotFoundException)

        when: 'But look you can add a new Schema at runtime!'

        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, 'foo')

        BookService bookService = new BookService()

        then:
        bookService.countBooks() == 0
        bookDataService.countBooks() == 0

        when: 'And the new @CurrentTenant transformation deals with the details for you!'
        bookService.saveBook('The Stand')
        bookService.saveBook('The Shining')
        bookService.saveBook('It')

        then:
        bookService.countBooks() == 3
        bookDataService.countBooks() == 3

        when: 'Swapping to another schema and we get the right results!'
        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, 'bar')
        bookService.saveBook('Along Came a Spider')
        bookDataService.saveBook('Whatever')
        then:
        bookService.countBooks() == 2
        bookDataService.countBooks() == 2
    }

}
